apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    useLibrary("org.apache.http.legacy")

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
            jniLibs.srcDirs = ['libs']
        }

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        config {
            storeFile file("../sign/z012v4_single.keystore")
            storePassword "z012v4single"
            keyAlias "z012v4"
            keyPassword "z012v4single"
        }
    }

    flavorDimensions "city"
    productFlavors {
        chengdu {
            dimension "city"
            applicationId "com.z012.chengdu.sc"
            versionCode 64
            versionName "4.0.4"
            /*manifestPlaceholders = [
                    "AppPackageName": applicationId
            ]*/
        }
        chongqing {
            dimension "city"
            applicationId "com.z012.chongqing.sc"
            versionCode 68
            versionName "4.0.4"
        }
    }

    productFlavors.all { flavor ->
        manifestPlaceholders.put("AppPackageName", applicationId)
    }

    buildTypes {
        release {
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
        }
        debug {
            signingConfig signingConfigs.config
            minifyEnabled false
        }
    }

    /*splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a','x86_64'
            universalApk true
        }
    }*/

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation project(path: ':library')
    //android support library
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    //glide
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.github.bumptech.glide:okhttp-integration:1.3.1@aar'
    //okhttp
    implementation 'com.squareup.okhttp:okhttp:2.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    //fastjson
    implementation 'com.alibaba:fastjson:1.2.51'
    //eventbus
    implementation 'org.greenrobot:eventbus:3.1.1'
    //leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.2'
    //ButterKnife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //下拉刷新布局
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.0.5.1'
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'org.ligboy.retrofit2:converter-fastjson-android:2.1.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    //rxjava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    //logger
    implementation 'com.orhanobut:logger:2.1.1'
}
